#!/usr/bin/env perl
#
# InspIRCd -- Internet Relay Chat Daemon
#
#   Copyright (C) 2021-2022, 2024 Sadie Powell <sadie@witchery.services>
#
# This file is part of InspIRCd.  InspIRCd is free software: you can
# redistribute it and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, version 2.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#


use v5.26.0;
use strict;
use warnings FATAL => qw(all);

use File::Basename        qw(dirname);
use File::Spec::Functions qw(catfile);
use FindBin               qw($RealDir);

use lib dirname $RealDir;
use make::common;
use make::console;

my %committers;
for my $committer (split /\n+/, `git log --pretty='%an <%ae>%n%(trailers:key=Co-Authored-By,valueonly)' HEAD`) {
	$committers{$committer} ||= 0;
	$committers{$committer} += 1;
}

my %authors;
for my $committer (keys %committers) {
	open(my $fh, '-|', 'git', 'check-mailmap', $committer);
	chomp(my $author = <$fh>);
	close $fh;

	$author = $1 if $author =~ /^(.+) <(?:unknown\@email.invalid|\S+\@users.noreply.github.com)>$/;
	next if $author =~ /\[bot\]$/;

	$authors{$author} ||= 0;
	$authors{$author} += $committers{$committer};
}

my $author_file = catfile dirname($RealDir), 'docs', 'AUTHORS.txt';
open(my $fh, '>', $author_file) or print_error "unable to write $author_file: $!";
say $fh <<"EOH";
Since the first commit in January 2003 ${\scalar %authors} people have submitted patches,
commits, and other useful contributions to InspIRCd. These people, ordered by
the number of contributions they have made, are:
EOH

for my $author (sort { $authors{$b} <=> $authors{$a} or lc($a) cmp lc($b) } keys %authors) {
	next if $author eq 'InspIRCd Robot <noreply@inspircd.org>';
	say $fh "  * $author";
}
close $fh;

if ($ENV{MKAUTHORS_COMMIT} // 1) {
	execute 'git', 'commit',
		'--author', 'InspIRCd Robot <noreply@inspircd.org>',
		'--message', 'Update author list.',
		'--', $author_file;
}
